!-----------------------------------------------------------------------------
! (c) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Test the common subgrid functions used for FFSL transport

module subgrid_common_support_mod_test

  use constants_mod,                  only: i_def, l_def, r_tran
  use transport_enumerated_types_mod, only: monotone_none,                     &
                                            monotone_strict,                   &
                                            monotone_relaxed,                  &
                                            monotone_positive,                 &
                                            monotone_qm_pos

  implicit none

contains

  !----------------------------------------------------------------------------

  @test
  subroutine monotonic_edge_test()
    use funit
    use, intrinsic :: iso_fortran_env,  only: real64
    use subgrid_common_support_mod, only: monotonic_edge

    implicit none

    integer(kind=i_def), parameter :: nlayers = 1

    real(kind=r_tran)   :: tol
    real(kind=r_tran)   :: rho(nlayers,1:3), edge_left(nlayers), edge_right(nlayers)
    real(kind=r_tran)   :: answer_left(nlayers), answer_right(nlayers), min_val
    integer(kind=i_def) :: monotone
    integer(kind=i_def), parameter :: order = 1

    ! Set tolerance
    if ( r_tran == real64 ) then
      tol = 1.0e-12_r_tran
    else
      tol = 1.0e-6_r_tran
    end if

    ! Set field data
    rho(1,1) = 1.0_r_tran
    rho(1,2) = 2.0_r_tran
    rho(1,3) = 4.0_r_tran
    min_val = 0.0_r_tran

    ! Set edge values
    edge_left(1) = -1.0_r_tran
    edge_right(1) = 4.5_r_tran

    ! Test with no monotonicity (edge values should not change)
    monotone = monotone_none
    answer_left(1)  = -1.0_r_tran
    answer_right(1) = 4.5_r_tran

    call monotonic_edge(                                                       &
            rho, monotone, min_val, edge_left, edge_right, order, 1, nlayers   &
    )
    @assertEqual(answer_left,  edge_left, tol)
    @assertEqual(answer_right, edge_right, tol)

    ! Reset edge values
    edge_left(1) = -1.0_r_tran
    edge_right(1) = 4.5_r_tran

    ! Test with strict monotonicity (edge values should be bounded)
    monotone = monotone_strict
    answer_left(1)  = 1.0_r_tran
    answer_right(1) = 4.0_r_tran

    call monotonic_edge(                                                       &
            rho, monotone, min_val, edge_left, edge_right, order, 1, nlayers   &
    )
    @assertEqual(answer_left,  edge_left, tol)
    @assertEqual(answer_right, edge_right, tol)

    ! Reset edge values
    edge_left(1) = -1.0_r_tran
    edge_right(1) = 4.5_r_tran

    ! Test with relaxed monotonicity (edge values should be bounded)
    monotone = monotone_relaxed
    answer_left(1)  = 1.0_r_tran
    answer_right(1) = 4.0_r_tran

    call monotonic_edge(                                                       &
            rho, monotone, min_val, edge_left, edge_right, order, 1, nlayers   &
    )
    @assertEqual(answer_left,  edge_left, tol)
    @assertEqual(answer_right, edge_right, tol)

    ! Reset edge values
    edge_left(1) = -1.0_r_tran
    edge_right(1) = 4.5_r_tran

    ! Test with positive monotonicity (edge values should be positive)
    monotone = monotone_positive
    answer_left(1)  = 0.0_r_tran
    answer_right(1) = 4.5_r_tran

    call monotonic_edge(                                                       &
            rho, monotone, min_val, edge_left, edge_right, order, 1, nlayers   &
    )
    @assertEqual(answer_left,  edge_left, tol)
    @assertEqual(answer_right, edge_right, tol)

    ! Reset edge values
    edge_left(1) = -1.0_r_tran
    edge_right(1) = 4.5_r_tran

    ! Test with quasi-monotonicity (edge values should be bounded by min_val)
    monotone = monotone_qm_pos
    answer_left(1)  = 0.0_r_tran
    answer_right(1) = 4.5_r_tran

    call monotonic_edge(                                                       &
            rho, monotone, min_val, edge_left, edge_right, order, 1, nlayers   &
    )
    @assertEqual(answer_left,  edge_left, tol)
    @assertEqual(answer_right, edge_right, tol)

  end subroutine monotonic_edge_test

  !-----------------------------------------------------------------------------

  @test
  subroutine subgrid_quadratic_recon_test()
    use funit
    use, intrinsic :: iso_fortran_env,  only: real64
    use subgrid_common_support_mod, only: subgrid_quadratic_recon

    implicit none

    integer(kind=i_def), parameter :: nlayers = 1

    real(kind=r_tran)   :: tol
    real(kind=r_tran)   :: rho(nlayers,1), edge_left(nlayers), edge_right(nlayers)
    real(kind=r_tran)   :: answer(nlayers), recon(nlayers), dep_pt(nlayers)
    integer(kind=i_def) :: monotone

    ! Set tolerance and switch monotonicity off
    if ( r_tran == real64 ) then
      tol = 1.0e-12_r_tran
    else
      tol = 1.0e-6_r_tran
    end if

    ! Set field data
    rho(1,1) = 2.0_r_tran
    edge_left(1)  = 4.0_r_tran
    edge_right(1) = 6.0_r_tran

    ! Test positive departure distance with no monotonicity
    monotone = monotone_none
    dep_pt(1) = 0.2_r_tran
    answer(1) = 4.24_r_tran

    call subgrid_quadratic_recon(recon, dep_pt, rho, edge_left, edge_right, monotone, 0, nlayers)
    @assertEqual(answer, recon, tol)

    ! Test positive departure distance with strict monotonicity
    monotone = monotone_strict
    dep_pt(1) = 0.2_r_tran
    answer(1) = 2.0_r_tran

    call subgrid_quadratic_recon(recon, dep_pt, rho, edge_left, edge_right, monotone, 0, nlayers)
    @assertEqual(answer, recon, tol)

    ! Test with swapped edge values (to represent negative flow)
    ! and no monotonicity
    monotone = monotone_none
    dep_pt(1) = ABS(-0.2_r_tran)  ! abs here to emphasise negative flow
    answer(1) = 2.64_r_tran
    edge_left(1)  = 6.0_r_tran
    edge_right(1) = 4.0_r_tran

    call subgrid_quadratic_recon(recon, dep_pt, rho, edge_left, edge_right, monotone, 0, nlayers)
    @assertEqual(answer, recon, tol)

    ! Test with swapped edge values (to represent negative flow)
    ! and positive monotonicity
    monotone = monotone_positive
    dep_pt(1) = ABS(-0.2_r_tran)  ! abs here to emphasise negative flow
    answer(1) = 2.64_r_tran

    call subgrid_quadratic_recon(recon, dep_pt, rho, edge_left, edge_right, monotone, 0, nlayers)
    @assertEqual(answer, recon, tol)

  end subroutine subgrid_quadratic_recon_test

end module subgrid_common_support_mod_test
