!-----------------------------------------------------------------------------
! (c) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Initialisation of fields for the name_transport miniapp.
!> @details The initialisation of the density, conservative tracer
!!          and wind fields

module name_transport_init_fields_alg_mod

  use constants_mod,                     only: r_def, i_def, radians_to_degrees
  use domain_mod,                        only: domain_type
  use field_mod,                         only: field_type
  use field_parent_mod,                  only: write_interface
  use finite_element_config_mod,         only: element_order_h, element_order_v
  use fs_continuity_mod,                 only: W2, W3
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use init_gungho_prognostics_alg_mod,   only: init_u_field
  use io_config_mod,                     only: write_diag, use_xios_io
  use lfric_xios_write_mod,              only: write_field_generic
  use mesh_mod,                          only: mesh_type
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type
  use sci_fem_constants_mod,             only: get_qr_fe
  use sci_geometric_constants_mod,       only: get_coordinates, &
                                               get_panel_id
  use set_name_field_kernel_mod,         only: set_name_field_kernel_type
  use set_rho_kernel_mod,                only: set_rho_kernel_type

  implicit none

  private

  public :: name_transport_init_fields_alg

contains

  !-----------------------------------------------------------------------------
  !> @brief Initialisation of prognostic fields for the name_transport miniapp.
  !> @param[in]     mesh              Mesh to initialise variables on
  !> @param[in,out] wind              Transporting wind field (W2)
  !> @param[in,out] density           Density field (in W3)
  !> @param[in,out] tracer_con        Conservative tracer field (in W3)
  !-----------------------------------------------------------------------------
  subroutine name_transport_init_fields_alg( mesh,         &
                                             wind,         &
                                             density,      &
                                             tracer_con )

    implicit none

    ! Arguments
    type(mesh_type), pointer, intent(in)    :: mesh
    type(field_type),         intent(inout) :: wind
    type(field_type),         intent(inout) :: density
    type(field_type),         intent(inout) :: tracer_con

    ! Internal variables
    real(kind=r_def), parameter         :: initial_time = 0.0_r_def
    type(domain_type) :: domain
    real(kind=r_def)  :: domain_max_x
    type(field_type),           pointer :: chi(:)   => null()
    type(field_type),           pointer :: panel_id => null()
    type(function_space_type),  pointer :: w2_fs    => null()
    type(function_space_type),  pointer :: w3_fs    => null()
    type(quadrature_xyoz_type), pointer :: qr       => null()
    procedure(write_interface), pointer :: tmp_write_ptr => null()

    ! Set pointers
    w2_fs => function_space_collection%get_fs( mesh, element_order_h, &
                                               element_order_v, W2 )
    w3_fs => function_space_collection%get_fs( mesh, element_order_h, &
                                               element_order_v, W3 )
    chi  => get_coordinates(mesh%get_id())
    panel_id => get_panel_id(mesh%get_id())
    qr => get_qr_fe()

    ! Get domain from mesh
    domain = mesh%get_domain()
    if ( domain%is_lonlat() ) then
      domain_max_x = domain%maximum_lonlat(axis=1) * radians_to_degrees
    else
      domain_max_x = domain%maximum_xy(axis=1)
    end if

    ! Initialise field objects
    call wind%initialise( name='u', vector_space=w2_fs )
    call density%initialise( name='rho', vector_space=w3_fs )
    call tracer_con%initialise( name='tracer_con', vector_space=w3_fs )

    ! ======================================================================== !
    ! Set up field values from analytic initial conditions
    ! ======================================================================== !

    ! Compute initial density and tracer_con
    call invoke( set_rho_kernel_type(        density,    chi, panel_id, &
                                             initial_time, qr ),        &
                 set_name_field_kernel_type( tracer_con, chi, panel_id, &
                                             domain_max_x, qr ) )

    ! Compute initial wind
    call init_u_field( wind, initial_time )

    ! ======================================================================== !
    ! Set I/O behaviours for diagnostic output
    ! ======================================================================== !
    if ( write_diag .and. use_xios_io ) then
       ! Fields that are output on the XIOS face domain
       tmp_write_ptr => write_field_generic
       call wind%set_write_behaviour( tmp_write_ptr )
       call density%set_write_behaviour( tmp_write_ptr )
       call tracer_con%set_write_behaviour( tmp_write_ptr )
    end if

    nullify( w2_fs, w3_fs, chi, panel_id, qr, tmp_write_ptr )

  end subroutine name_transport_init_fields_alg

end module name_transport_init_fields_alg_mod
